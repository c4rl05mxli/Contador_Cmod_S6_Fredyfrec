prompt_input:   ;CALL send_prompt                         ;Prompt 'KCPSM3>'
                CALL receive_string      
	

read_Command:	CALL read_from_UART      	; Lee el caracter 1 de la uart
				COMPARE sF, 02          	; Verifico que sea un STX
				JUMP NZ, bad_command
				CALL read_from_UART			; Lee el caracter 2 de la uart
				STORE sF, COMANDO			; lo guardo en la memoria
				CALL read_from_UART      	; Lee el caracter de la uart
				COMPARE sF, 03          	; Verifico que tengo un STX
				JUMP NZ, bad_command
				RETURN

bad_command:	LOAD s0,FF
				STORE s0,COMANDO
				CALL clear_UART_Rx_loop
				RETURN
;**************************************************************************************
;***	Lee un caracter por la UART
;***	usa los registros s0 y sF
;**************************************************************************************
				;
read_from_UART: INPUT s0, UART_status_port   	; test Rx_FIFO buffer
				TEST s0, rx_data_present
				JUMP NZ, read_character
				JUMP read_from_UART
read_character: INPUT sF, UART_read_port  		; read from FIFO
				RETURN
				;
;**************************************************************************************
;***	Transmite un caracter por la UART
;***	usa los registros s0 y sF
;**************************************************************************************
				;
send_to_UART: 	INPUT s0, UART_status_port      ; verifico si hay especio en buffer
				TEST s0, tx_full
				JUMP Z, UART_write				; si hay espacio transmito	
				JUMP send_to_UART				; si no hay espacio espero
UART_write: 	OUTPUT sF, UART_write_port
				RETURN
				;
;**************************************************************************************
;***	Transmite Cadena envia loa valores almacenados en las 9 posisiones de memoria
;***	usa los registros sE y sF
;**************************************************************************************
;transmit_string:	LOAD sE, string_start     		; Carga en sE, el valor 0x20, posición del MSD      
; next_char_tx: 	FETCH sF, (sE)                 ; cargo el sF, la posicion de memoria apuntada por sE
					; CALL send_to_UART              ; envio el valor de sF por la UART  
					; COMPARE sE, string_end         ; comparo con el final de la cadena = 0x28
					; RETURN Z
					; ADD sE, 01                       
					; JUMP next_char_tx
;**************************************************************************************
;	
;**************************************************************************************
;***	Transmite Cadena envia loa valores almacenados en las 9 posisiones de memoria
;***	usa los registros sE y sF (sD modo de operacion)
;**************************************************************************************
transmit_string:	FETCH sD, MODO
					LOAD sE, string_start     		; Carga en sE, el valor 0x20, posición del MSD      
next_char_tx: 		FETCH sF, (sE)                 ; cargo el sF, la posicion de memoria apuntada por sE
					CALL send_to_UART              ; envio el valor de sF por la UART  
					COMPARE sE, string_end         ; comparo con el final de la cadena = 0x28
					RETURN Z
					ADD sE, 01 
					COMPARE sE,23					; pone el punto en el caracter 23
					JUMP NZ	, next_char_tx
					COMPARE sD,02
					JUMP NZ	, next_char_tx			; si estoy en el modo 2 pongo el punto
					LOAD sF, "." 
					CALL send_to_UART
					JUMP next_char_tx
;**************************************************************************************













		
;				
;**************************************************************************************
;***	Transmite Cadena envia loa valores almacenados en las 9 posisiones de memoria
;***	usa los registros sE y sF
;**************************************************************************************
transmit_spaces:	;LOAD s5, 44          		 ; Linea 4 posision 5
					;CALL LCD_cursor
					LOAD sE, 00    				; Carga en sE, el valor 0x20, posición del MSD      
next_space: 		LOAD sF, " "    ; cargo el sF, la posicion de memoria apuntada por sE
					CALL send_to_UART         ; envio el valor de sF por la UART  
					COMPARE sE, 20        ; comparo con el final de la cadena = 0x28
					RETURN Z
					ADD sE, 01                       
					JUMP next_space
;**************************************************************************************
					
					
;**************************************************************************************
;***		Función para leer datos desde el teclado y genera string de datos		***
;***		Utiliza las constantes string_start y string_end						***
;***		Registros usados s0, sF													***
;**************************************************************************************					
receive_string: 	;
receive_full_test: 	;
					INPUT s0, UART_status_port   ; verifico si Buffer UART esta lleno
					TEST s0, rx_full
					JUMP NZ, clear_UART_Rx_loop
					CALL read_from_UART      	; Lee el caracter de la uart
					;								
					COMPARE sF, 03          	; Si presiono la tecla ETX, es el Enter
					JUMP Z, check_zeros
					COMPARE sF, 02          	; Verifico que tengo un STX
					JUMP Z, clear_all
					;****** Verifico que solo sean caracteres entre 0 y 9 ********************
					LOAD s0 , sF
					ADD s0, C6                      ; reject character codes above '9' (39 hex)
					JUMP C, receive_full_test		; carry flag is set
					SUB s0, F6                      ; reject character codes below '0' (30 hex)
					JUMP C, receive_full_test		; si no hay acarreo el dato esta entre 0 y 9
					;***************************************************************************
					CALL move_left
					STORE sF, string_end     		; Guardo siempre en la ultima posicion
					JUMP receive_full_test	        ; despues regreso a solicitar otro dato
					;
					;
clear_all: 			CALL Fill_zeros
					JUMP receive_full_test
					
			
check_zeros:	LOAD s0, string_end
check_loop: 	FETCH s1,(s0)
                COMPARE s1,"0"
                RETURN NZ
                COMPARE s0, string_start
                JUMP Z,receive_full_test
                SUB s0,01
                JUMP check_loop
			
					
					
					
clear_UART_Rx_loop: INPUT s0, UART_status_port           ;test Rx_FIFO buffer for data
					TEST s0, rx_data_present
					RETURN Z                              ;finish when buffer is empty
					INPUT sF, UART_read_port          ;read from FIFO and ignore
					JUMP clear_UART_Rx_loop
                    ;
;**************************************************************************************			   
		   
	
;**************************************************************************************
;***		Función para mover string a la izquierda								***
;***		Utiliza las constantes string_start y string_end						***
;***		Registros usados s0, s1 y s2											***
;**************************************************************************************
					;
	move_left:  	LOAD s0, string_start
    ml_loop:    	LOAD s1, s0
                    ADD s1,01
                    FETCH s2,(s1)
                    STORE s2,(s0)
                    COMPARE s1,string_end
                    RETURN Z
                    ADD s0,01
                    JUMP ml_loop 
					;
;**************************************************************************************
   
;**************************************************************************************
;***		Función para mover string a la izquierda								***
;***		Utiliza las constantes string_start y string_end						***
;***		Registros usados s0, s1 y s2											***
;**************************************************************************************
					;   
     move_right:  	LOAD s0, string_end
        mr_loop:    LOAD s1, s0
                    SUB s1,01
                    FETCH s2,(s1)
                    STORE s2,(s0)
                    SUB s0,01
                    COMPARE s1,string_start
                    JUMP Z, move_zero
                    JUMP mr_loop
        move_zero:  LOAD s2, "0"
                    STORE s2,(s0)
                    RETURN
					;
;**************************************************************************************
   		   
		   
;**************************************************************************************
;***		Función para llenar string de datos										***
;***		Utiliza las constantes string_start y string_end						***
;***		Registros usados s0 y s1												***
;**************************************************************************************
					;
	Fill_zeros: 	LOAD s0, "0"
	Fill_s0:  		LOAD s1, string_start		; Usa el valor de s0
	Fill:			STORE s0, (s1)
					COMPARE s1,string_end
					RETURN Z
					ADD s1,01
					JUMP Fill
					;
;**************************************************************************************
				
				
;**************************************************************************************
;***		Función para convertir string de ASCII a BCD							***
;***		Constantes Usadas:	string_start, string_end y BCD_digit0				***
;***		Registros Usados: 	s0, s1 y s2											***
;**************************************************************************************
					;
	ascii_to_bcd:	LOAD s0, string_end			; string_end
					LOAD s1, BCD_digit0			; BCD_digit0 
	ascii_loop:     FETCH s2, (s0)				; Leo de la Memoria
					AND   s2, 0F				; Obtengo el nibble Bajo
					STORE s2,(s1)				; Escribo en la Memoria
					COMPARE s0,string_start	; Comparo con el Ultimo Caracter
					RETURN Z					; Regreso si es Cero
					SUB s0, 01					; Redusco el puntero de String
					ADD s1,01					; Aumento el puntero de BCD
					JUMP ascii_loop				; 
					;
;**************************************************************************************				

;**************************************************************************************
;***		Funcion para convertir entero de 32-bit a BCD							***
;***		Constantes Usadas: decimal0												***
;***		Registros Usados: s0,s2,s3,s4,s5,s6,s7,s8,s9,sA,sB,sC,sD,sE,sF			***
;***		Registros de Entrada:	[s5,s4,s3,s2]									***
;***							MSByte	  		LSByte								***
;**************************************************************************************
					;
	integer_to_BCD: LOAD sE, 0A                 ;10 digits to be formed from value up to 4294967295
					LOAD sF, decimal0           ;pointer for LS-Digit
   int_to_BCD_loop: CALL divide_32bit_by_10
					STORE s1, (sF)              ;remainder becomes digit value
					ADD sF, 01                  ;move to next most significant digit
					SUB sE, 01                  ;one less digit to compute
					JUMP NZ, int_to_BCD_loop
					RETURN
					;
;Divide 32-bit binary integer by 10
;
;The value to be divided is held in register set [s5,s4,s3,s2]
;and this is where the result is returned to.
;
;At then end of the integer division the remainder in the range 0 to 9
;will be in register s1.
;
;Registers used s0, s2,s3,s4,s5,s6,s7,s8,s9,sA,sB,sC,sD
					;
divide_32bit_by_10: LOAD sA, s2                         ;copy input value to set [sD,sC,sB,sA]
					LOAD sB, s3
					LOAD sC, s4
					LOAD sD, s5
					LOAD s2, 00                         ;clear result
					LOAD s3, 00
					LOAD s4, 00
					LOAD s5, 00
					LOAD s9, A0                         ;initialise '10' value into msb's of set [s9,s8,s7,s6]
					LOAD s8, 00
					LOAD s7, 00
					LOAD s6, 00
					LOAD s0, 1D                         ;29 subtract and shift iterations to be performed
		div10_loop: SUB sA, s6                          ;perform 32-bit subtract [sD,sC,sB,sA]-[s9,s8,s7,s6]
					SUBCY sB, s7
					SUBCY sC, s8
					SUBCY sD, s9
					JUMP C, div10_restore
					SL1 s2                              ;shift '1' into result
					JUMP div10_shifts
	 div10_restore: ADD sA, s6                          ;perform 32-bit addition [sD,sC,sB,sA]+[s9,s8,s7,s6]
					ADDCY sB, s7
					ADDCY sC, s8
					ADDCY sD, s9
					SL0 s2                              ;shift '0' into result
	  div10_shifts: SLA s3                              ;complete 32-bit shift left
					SLA s4
					SLA s5
					SR0 s9                              ;divide '10' value by 2 (shift right 1 place)
					SRA s8
					SRA s7
					SRA s6
					SUB s0, 01                          ;count iterations
					JUMP NZ, div10_loop
					LOAD s1, sA                         ;remainder of division
					RETURN
;**************************************************************************************
				


;
;
;
;**************************************************************************************
; Convert 9 digit BCD frequency into 32-bit binary integer
;**************************************************************************************
;
;	Both values are stored in scratch pad memory
;    BCD values in ascending locations BCD_digit0 to BCD_digit8
;    Binary frequency in ascending locations frequency0 to frequency3
;
;Each digit is read in turn and its value is determined by repeated
;decrement until reaching zero. Each decrement causes a value to be added
;to the memory locations forming the frequency value as binary integer.
;The process requires approximately 1600 instructions to convert the highest
;value 999,999,999 which is approximately 64us at 50MHz clock rate.
;
;Registers used s0,s1,s2,s3,s4,s5,s6,s7,s8,s9,sA,sB
;
BCD_to_integer:     LOAD s2, 09                         ;9 digits to convert
					LOAD s0, 00                         ;clear frequency value ready to accumulate result
					STORE s0, frequency0
					STORE s0, frequency1
					STORE s0, frequency2
					STORE s0, frequency3
					LOAD sB, 00                         ;initialise BCD digit weighting [sB,sA,s9,s8] to 1
					LOAD sA, 00
					LOAD s9, 00
					LOAD s8, 01
					LOAD s3, BCD_digit0                 ;locate LS-digit
next_BCD_to_int_digit: FETCH s1, (s3)
BCD_digit_convert: COMPARE s1, 00                      ;test for zero
					JUMP Z, next_digit_value
					FETCH s0, frequency0                ;add 32-bit digit weighting to memory value
					ADD s0, s8
					STORE s0, frequency0
					FETCH s0, frequency1
					ADDCY s0, s9
					STORE s0, frequency1
					FETCH s0, frequency2
					ADDCY s0, sA
					STORE s0, frequency2
					FETCH s0, frequency3
					ADDCY s0, sB
					STORE s0, frequency3
					SUB s1, 01                          ;decrement digit value
					JUMP BCD_digit_convert
					;Increase weighting by 10x
next_digit_value:   LOAD s7, sB                         ;copy existing weighting
					LOAD s6, sA
					LOAD s5, s9
					LOAD s4, s8
					SL0 s8                              ;multiply weight by 4x (shift left 2 places)
					SLA s9
					SLA sA
					SLA sB
					SL0 s8
					SLA s9
					SLA sA
					SLA sB
					ADD s8, s4                          ;add previous weight to form 5x multiplication
					ADDCY s9, s5
					ADDCY sA, s6
					ADDCY sB, s7
					SL0 s8                              ;multiply weight by 2x (shift left 1 places)
					SLA s9
					SLA sA
					SLA sB                              ;weight value is now 10x previous value
					ADD s3, 01                          ;move to next digit for conversion
					SUB s2, 01
					JUMP NZ, next_BCD_to_int_digit
					RETURN
					;
;**************************************************************************************
;***		Función para convertir enviar digitos decimales por la UART				***
;***		Constantes Usadas:	decimal9 a decimal0									***
;***		Registros Usados: 	sE, UART(s0 y sF)									***
;**************************************************************************************	
					;
disp_digits_timer:  FETCH sF, decimal7                  ; 100mS
                    CALL disp_digit_timer
                    FETCH sF, decimal6                  ; 10mS
                    CALL disp_digit_timer
                    FETCH sF, decimal5                  ; 1mS
                    CALL disp_digit_timer
                    FETCH sF, decimal4                  ; 100us
                    CALL disp_digit_timer
                    FETCH sF, decimal3                  ; 10uS
                    CALL disp_digit_timer
					FETCH sF, decimal2                 	; 1us
                    CALL disp_digit_timer
                    ;FETCH sF, decimal1         			; 100nS
                    ;CALL disp_digit_timer
                    ;FETCH sF, decimal0  				; 10nS
					;CALL disp_digit_timer
                    RETURN
					;
disp_digit_timer:   ADD sF, "0"                 ;convierto el numero a ASCII
                    CALL send_to_UART
                    RETURN	  
					;
;**************************************************************************************
;***		Función para convertir y enviar digitos por la UART						***
;***		Constantes Usadas:	decimal9 a decimal0									***
;***		Registros Usados: 	sE, UART(s0 y sF)									***
;**************************************************************************************					  	
					;
disp_digits: 		LOAD sE, FF                         ;bandera de caracter nulo       	  
					FETCH sF, decimal9                  ;
					CALL zero_test
					CALL disp_digit
					FETCH sF, decimal8                  ;
					CALL zero_test
					CALL disp_digit
					FETCH sF, decimal7                  ;
					CALL zero_test
					CALL disp_digit
					FETCH sF, decimal6                  ;
					CALL zero_test
					CALL disp_digit
					FETCH sF, decimal5                  ;
					CALL zero_test
					CALL disp_digit
					FETCH sF, decimal4                  ;
					CALL zero_test
					CALL disp_digit
					FETCH sF, decimal3                  ;
					CALL zero_test
					CALL disp_digit
					FETCH sF, decimal2                  ;
					CALL zero_test
					CALL disp_digit
					FETCH sF, decimal1                  ;
					CALL zero_test
					CALL disp_digit
					FETCH sF, decimal0  				   ;
					ADD sF, "0"                 ;
					CALL send_to_UART
					RETURN
					;
        zero_test:  COMPARE sF, 00		; compara para ver si es cero
                    RETURN Z
                    LOAD sE, 00			; si es cero cambio la bandera
					RETURN
                    ;
        disp_digit: COMPARE sE, FF
                    JUMP Z, blank_digit
                    ADD sF, "0"                 ;
                    CALL send_to_UART
                    RETURN
       blank_digit: ;LOAD sF, "0"				; imprimo los zeros
                    ;CALL send_to_UART
                    RETURN
;**************************************************************************************	